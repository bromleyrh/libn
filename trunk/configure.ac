#
# configure.ac
#

env_cflags="$CFLAGS"

AC_INIT([libn], m4_esyscmd_s([cat .version]))
AC_PREREQ([2.69])

AC_CONFIG_SRCDIR([add.S])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_MACRO_DIR([m4])

AC_CANONICAL_TARGET

dnl ensure installed files are accessible by non-root users when parent
dnl directories are created
dnl FIXME: Honor file mode creation mask for non-installation directories
MKDIR_P="build-aux/mkpath -m 0755"

AM_INIT_AUTOMAKE([foreign no-define subdir-objects])
AM_SILENT_RULES([yes])
LT_INIT([disable-static])

case $target_os in
    *darwin*)
        platform=darwin;;
    *freebsd*)
        platform=freebsd;;
    *linux*)
        platform=linux;;
    *)
        AC_MSG_ERROR([unsupported platform], [1]);;
esac
AM_CONDITIONAL([APPLE], [test "x$platform" = "xdarwin"])
AM_CONDITIONAL([FREEBSD], [test "x$platform" = "xfreebsd"])
AM_CONDITIONAL([LINUX], [test "x$platform" = "xlinux"])

AM_PROG_AS
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

AC_CHECK_PROGS([pkgcfg], [pkg-config pkgconf])

AX_LD_VERSION_SCRIPT

AC_ARG_ENABLE([debugging],
              [  --enable-debugging      enable debugging],
              [case "$enableval" in
                   yes)
                       debug=true;;
                   no)
                       debug=false;;
                   *)
                       AC_MSG_ERROR([invalid "--enable-debugging" argument]);;
               esac],
              [debug=false])
AM_CONDITIONAL([DEBUG], [test x$debug = xtrue])

AC_ARG_WITH([pkgconfigdir],
            [  --with-pkgconfigdir=DIR install pkg-config data in DIR],
            [pkgconfigdir="$withval"],
            [pkgconfigdir="$libdir/pkgconfig"])
AC_SUBST([pkgconfigdir])

AC_SUBST([ln_cur], [0])
AC_SUBST([ln_age], [0])
AC_SUBST([ln_rev], [0])

dnl FIXME: Later, only prevent Autoconf from adding "-g -O2" to CFLAGS
CFLAGS="$env_cflags"

AC_CONFIG_FILES([Makefile include/Makefile libn.pc])
AC_CONFIG_HEADERS([config.h])
AC_OUTPUT

cat <<- EOF

$PACKAGE_NAME configuration

    Target architecture: $target_cpu

               Compiler: $CC

              Debugging: $debug

    Installation prefix: $prefix

EOF

dnl vi: set expandtab sw=4 ts=4:
